//------------------------------------------------------------------------
// SIMULATE A POPULATION WITH CONSTANT SIZE AND MATING PREFERENCE FOR KIN
//------------------------------------------------------------------------

// set up a simple neutral simulation
initialize()
{
	// track pedigree information
	initializeSLiMOptions(keepPedigrees = T);

	// set the overall mutation rate
	initializeMutationRate(1.25e-8);
	
	// m1 mutation type: neutral
	initializeMutationType("m1", 0.0, "f", 0.0);
	
	// g1 genomic element type: uses m1 for all mutations
	initializeGenomicElementType("g1", m1, 1.0);
	
	// uniform chromosome of length 100 Mb
	initializeGenomicElement(g1, 0, 99999999);
	
	// uniform recombination along the chromosome
	initializeRecombinationRate(1e-8);
}

mateChoice() {
	// Prefer relatives as mates
	return weights * (individual.relatedness(sourceSubpop.individuals) + 0.01);
}

// create a population of 1000 individuals
1 { 
	sim.addSubpop("p1", 1000);
	writeFile("population.txt", "GEN\tN\tHeterozygosity");
}

// print population size
1:10000 late() {
	GEN = sim.generation;
	if (GEN % 1000 == 0) {
		POPSIZE = p1.individualCount;
		HET = calcHeterozygosity(p1.genomes);

		// print population info
		writeFile("population.txt", paste(c(asInteger(GEN), asInteger(POPSIZE), format("%.6f", HET)), sep="\t"), append=T);
	}
}

// run to generation
10000 late() {
	p1.outputVCFSample(100, filePath = "simulated_chr22_random_mating.vcf");
}

