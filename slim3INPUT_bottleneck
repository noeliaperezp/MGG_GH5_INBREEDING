//----------------------------------------------------------------------
// SIMULATE A POPULATION BOTTLENECK 50 GENERATIONS AGO
//----------------------------------------------------------------------

// set up a simple neutral simulation
initialize()
{
	// set the overall mutation rate
	initializeMutationRate(1.25e-8);
	
	// m1 mutation type: neutral
	initializeMutationType("m1", 0.0, "f", 0.0);
	
	// g1 genomic element type: uses m1 for all mutations
	initializeGenomicElementType("g1", m1, 1.0);
	
	// uniform chromosome of length 100 Mb
	initializeGenomicElement(g1, 0, 99999999);
	
	// uniform recombination along the chromosome (1cM/Mb)
	initializeRecombinationRate(1e-8);
}

// create a population of 1000 individuals and bottleneck
1 { 
	sim.addSubpop("p1", 1000);
	writeFile("slim3INPUT_bottleneck.txt", "GEN\tN\tHeterozygosity");
}
9950 { p1.setSubpopulationSize(50); }
9990 { p1.setSubpopulationSize(1000); }




// print population size
1:10000 late() {
	GEN = sim.generation;
	if (((GEN <= 9000) & (GEN % 1000 == 0)) | 
	    ((GEN > 9000) & (GEN <= 9900) & (GEN % 100 == 0)) |
	     ((GEN > 9900) & (GEN <= 10000) & (GEN % 10 == 0))) {
		POPSIZE = p1.individualCount;
		HET = calcHeterozygosity(p1.genomes);

		// print population info
		writeFile("slim3INPUT_bottleneck.txt", paste(c(asInteger(GEN), asInteger(POPSIZE), format("%.6f", HET)), sep="\t"), append=T);
	}
}

// run to generation
10000 late() {
	p1.outputVCFSample(p1.individualCount, replace = F, filePath = "slim3INPUT_bottleneck.vcf");
	//p1.outputVCFSample(100, filePath = "slim3INPUT_bottleneck.vcf");

}

